# Maintainer: Dennis Reimers <d.reimers@tu-bs.de>

pkgname=eagle-automation-git
pkgver=20140827
pkgrel=1
pkgdesc="Set of commandline tools for use with CadSoft Eagle (eagleexport,eaglediff, etc)"
arch=('any')
depends=('python2')
makedepends=('git' 'python2-setuptools' )
url="https://github.com/avian2/eagle-automation"
license=('GPL')
source=(${pkgname%-git}::git+https://github.com/avian2/eagle-automation)
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing python version to use...'
  find . -type f -print0 | xargs -0 sed -i "s/python$/python2/"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
